#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

if [[ -f /etc/profile ]]; then
  source /etc/profile
fi

#
# Browser
#

if [[ -z "$BROWSER" && "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

if [[ -z "$EDITOR" ]]; then
  export EDITOR=',emacsclient-alt-vim'
fi
if [[ -z "$VISUAL" ]]; then
  export VISUAL=',emacsclient-alt-vim'
fi
if [[ -z "$PAGER" ]]; then
  export PAGER='less'
fi

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
  export LC_COLLATE=C
  export LC_NUMERIC=C
fi

#
# Paths
#

typeset -T INFOPATH infopath

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath infopath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  $HOME/{,s}bin(N)
  /opt/{homebrew,local}/{,s}bin(N)
  /usr/local/{,s}bin(N)
  $path
  ~/.cargo/bin
)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X to enable it.
if [[ -z "$LESS" ]]; then
  export LESS='-g -i -M -R -S -w -X -z-4'
fi

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if [[ -z "$LESSOPEN" ]] && (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

fpath=(
  ~/.zfunc
  $fpath
)

# Disable Homebrew phoning home
export HOMEBREW_NO_ANALYTICS=1

export GPG_TTY=$(tty)
export COPYFILE_DISABLE=1 # prevent tar from adding ._* files

export RIPGREP_CONFIG_PATH=~/.ripgreprc

export FZF_DEFAULT_COMMAND='rg --files --hidden --follow --glob "!.git/*"'

export FZF_DEFAULT_OPTS='
  --color fg:-1,bg:-1,hl:33,fg+:254,bg+:235,hl+:33
  --color info:136,prompt:136,pointer:230,marker:230,spinner:136
'

export RSYNC_PROTECT_ARGS=1

if (( $+commands[patdiff-git-wrapper] )); then
  export GIT_EXTERNAL_DIFF=patdiff-git-wrapper
fi

path=(
  ~/bin
  $path
)

zprofile_sourced=1

# Redundant with .zshenv as a workaround for:
# - https://github.com/NixOS/nix/issues/4169
# - https://github.com/LnL7/nix-darwin/issues/122
if [ -e ~/.nix-profile/etc/profile.d/nix.sh ]; then
  . ~/.nix-profile/etc/profile.d/nix.sh
fi

if [ -e ~/.nix-profile/etc/profile.d/hm-session-vars.sh ]; then
  . ~/.nix-profile/etc/profile.d/hm-session-vars.sh
fi
